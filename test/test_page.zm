/******************************************************************************\
FILE:           test_page.zm
AUTHOR:         Theo Veenker <theo.veenker@beexy.nl>
ADAPTED BY:     -

DESCRIPTION:

Provides a page object to show to the participant during test trials.
It handles presenting the stimulus and recording the participant's response.


HISTORY:
2012-04-19 TV   Created.
2017-01-09 TV   Adapted for Zep 2.0.

\******************************************************************************/

import std_sound_output_device;


Page test_page
{
    TestItem    item;           // trial control parameters


    init()
    {
        fill_pattern_color = TEST_PAGE_COLOR;
    }


    on_event:message()
    {
        // Process response box message. See setup() below.
        response.process_hit(event_time);
    }


    VerticalLayout layout
    {
        init()
        {
            spacing = 50;
            height = 0;     // 0 means as large as possible
        }


        CanvasGadget canvas
        {
            init()
            {
                fill_pattern_color = TEST_PAGE_STIMULUS_COLOR;
                size = 700, 700;
                offset_x = width / 2;
                offset_y = height / 2;
            }


            CrossShape fixation
            {
                init()
                {
                    radius = 15;
                    line_width = 5;
                    line_pattern_color = color:crimson;
                }
            }

            ImageShape cue
            {
                init()
                {
                    width = 200;
                    height = 133;
                    offset_x = -width/2;
                    offset_y = -height/2;
                }

                void load(string cue_fn)
                {
                    image = cue_fn;
                }
            }

            ImageShape stim_cue
            {
                init()
                {
                    width = 100;
                    height = 100;
                    offset_x = -width / 2;
                    offset_y = -200 - height/2;
                }

                void load(string stim_cue_fn)
                {
                    image = stim_cue_fn;
                }
            }


            ImageShape stimulus
            {
                init ()
                {
                    width = 200;
                    height = 200;
                    offset_x = -width/2;
                    offset_y = -height/2;
                }

                void load(string fn)
                {
                    image = fn;
                }
            }
        }

        time start_stimulus(time t)
        {
            // Prepare stimulus presentation object.
            string imgdir = stimuli_dir() + "images/";
            string stimfn = imgdir + "PICTURE_" + item.pic_num + ".png";
            string cue_fn = imgdir + item.cue + 1 + ".png";
            string stim_cue_fn = imgdir + item.cue + 2 + ".png";

            canvas.cue.load(cue_fn);
            canvas.stim_cue.load(stim_cue_fn);
            canvas.stimulus.load(stimfn);

            // Start fixation cross.
            canvas.fixation.start(t, FIXATION_DURATION);
            time fixoffset = canvas.fixation.expected_finish_time;

            canvas.cue.start(fixoffset, CUE_DURATION);
            time cueoffset = canvas.cue.expected_finish_time;

            // Start stimulus and stim_cue at fixation offset.
            canvas.stim_cue.start(cueoffset, STIMULUS_DURATION);
            canvas.stimulus.start(cueoffset, STIMULUS_DURATION);

            // Return stimulus onset time.
            return canvas.stimulus.expected_start_time;
        }


        void reset()
        {
            full_abort();

            canvas.stimulus.is_visible = false;
        }
    }


    SoundPlayback beeper
    {
        // Sound source/producer object.
        BeepGenerator beep {}


        time play(time t)
        {
            abort();

            device = sound_output_device;
            beep.duration = 100ms;
            beep.frequency[0] = 1000;
            beep.scaling[0] = 0.5;
            beep.fade_in_duration = 1ms;
            beep.fade_out_duration = 1ms;

            start(t);

            return expected_finish_time;
        }
    }


    Response response
    {
        void clear()
        {
        }


        void process_hit(time t)
        {
            HitType validity = hit(t);

            // Accept the first hit in the response interval and end the
            // response interval.
            if (validity == HIT_VALID)
            {
                ignore_remaining_hits();

                stop();
            }
            else if (validity == HIT_TOO_EARLY || validity == HIT_TOO_LATE)
            {
                // False alarm. Deal with this if desired.
            }
        }


        on_event:finish()
        {
            done(CONTINUE);
        }
    }


    // Aborts any ongoing activity on this page and signals the initiator
    // of the trial that we're done.
    void done(int msgid)
    {
        // Just in case; abort presentation if still active.
        layout.reset();

        signal_target(msgid);   // tell caller we're done
        target = null;

        control.clear_status();
    }


    //==========================================================================


    // Performs preparatory work required before using this page.
    void setup(int ntrials=-1, bool pr=false)
    {
        test_page_overlay.setup(this);

        // Show or hide prompt.
        test_page_overlay.show_prompt(pr ?
            "Name each object in the language indicated by the flag" : "");

        // Enable or disable the progress bar.
        test_page_overlay.show_progress_bar(ntrials);   // disabled if < 0


        // Enable response box buttons and fallback keyboard keys.
        control.target = this;
        control.disable_buttons();
        control.disable_keys();
        control.enable_all_buttons();
//        control.enable_button(BUTTON_?);
        control.enable_key(KEY_Return);
        control.enable_key(' ');
    }


    // Performs cleaning up if necessary.
    void cleanup()
    {
        test_page_overlay.cleanup();

        control.disable_buttons();
        control.disable_keys();
        control.target = null;
    }


    // Starts a trial.
    void action(Object caller, TestItem it, time tref, int count=-1)
    {
        // Save the passed trial control parameters.
        item = it;


        // Show this page (if not yet done) and make tref the trial start time.
        tref = test_window1.show_test_page(this, tref + INTERTRIAL_INTERVAL);


        // Reset response data.
        response.clear();


        // Start fixation symbol and stimulus.
        time stimonset = layout.start_stimulus(tref);


        // Start beep at stimulus onset.
        beeper.play(stimonset);


        // Response interval starts at stimulus onset. Timeout after
        // RESPONSE_DURATION unless this duration is 0s or less.
        response.start(stimonset, RESPONSE_DURATION);


        // Update progress counter (if enabled).
        test_page_overlay.update_progress_bar(count);


        // Update status line on control window. Cleared in done() above.
        control.set_status(
            string(item.id) + "  " +
            string(item.type) + "  " +
            string("PICTURE_" + item.pic_num));


        // Remember who to signal when the trial is over.
        target = caller;
    }
}
